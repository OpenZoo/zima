plugins {
    id 'java'
    id "io.freefair.lombok" version "8.13.1"
    id 'com.github.johnrengelman.shadow' version "8.1.1"
    id "com.github.hierynomus.license" version "0.16.1"
}

group 'pl.asie.zima'

version = '0.9.0'
ext.revision = 'git rev-list --count HEAD'.execute().text.trim()
ext.hash = 'git rev-parse --short HEAD'.execute().text.trim()
version = "${version}-r${revision}.${hash}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jcommander:jcommander:1.85'
    implementation 'com.github.davidmoten:word-wrap:0.1.12'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'com.google.guava:guava:33.4.8-jre'

    testImplementation(platform('org.junit:junit-bom:5.13.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

license {
    header = file('docs/licenses/COPYING')
    exclude "**/*.txt"
    exclude "**/asie/zima/repack/**"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "docs"
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'pl.asie.zima.Main'
    }
}

artifacts {
    shadowJar
}

shadowJar {
    minimize()
}

build.dependsOn(shadowJar)

tasks.withType(Copy).all {
    duplicatesStrategy 'exclude'
}
